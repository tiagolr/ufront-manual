Model, View, Controller, API
============================

The "Model, View, Controller" pattern is a common way of structuring apps, and is particularly suitable for building websites and web apps.
It gives you a plan to organise your code in a way where each class is responsible for one thing, and one thing only.

As well as the usual "Model, View, Controller" setup, we have also added APIs, so instead of MVC, we have MVCA.

### Overview

Here is a breakdown of what we mean by each of the terms "Model", "View" and "Controller". We have used a consistent example of building a simple blog, so that in the example code you can see how these pieces interact with each other.

#### Models

> __A *Model* is all about the data in your app.__

The model defines how your data is structured, what is valid (or invalid) data, and how to save this data to make it permanent.

For example you might have a BlogPost model. In Ufront this will typically be a class, which tells you which fields a blog post must have: let's say it must have a title, an author, a publishing date, and some text for the post.
You might also have other models for other types of data on the blog - an Author model, a Comment model, a Category model etc.

A model will also take care of validation rules - perhaps you have a rule that an author must be specified, or that a title cannot be more than 1 line of text.

Finally, the model will have a way of storing the data.
This might be saving to a database (this is probably the most common) or something else - saving to a file, writing to the user's browser, interacting with a 3rd party API, or any number of things.

If you were creating a blog you might also have models for an Author, a Comment, a Category etc.

The idea is that when you build your app, you start by defining what your data looks: you create the models.
Then the framework helps you take care of saving and retrieving that data for you, and you can use these data objects throughout your app knowing exactly what they look like and how they'll function.

In ufront, a database model goes in it's own class. Following the above example, **BlogPost.hx**:

```haxe
import sys.db.Types;
import ufront.db.ManyToMany;
class BlogPost extends ufront.db.Object {
	public var title:SString<100>;
	public var date:Date;
	public var text:SText;
	public var author:BelongsTo<Author>;
	public var comments:HasMany<Comment>;
	public var categories:ManyToMany<BlogPost,Category>;
}
```

You can then use your models very naturally in Haxe code:

```
// Get all blog posts
var list = BlogPost.manager.all();

// Get all posts newer than Jan 1 2014
var list = BlogPost.manager.search( $date>new Date(2014,0,1) );

// Get just one post with the title "Sorry for not posting"
var blogPost = BlogPost.manager.select( $title=="Sorry for not posting" );

// Change some data and save it back.
blogPost.text = "I'm not actually sorry. Nobody reads this blog anyway."
blogPost.save();

// Decide you actually don't want to post this.
blogPost.delete();
```

To learn more about using Models in Ufront, see [[05.0.0 Model]].

#### Controllers

> __A *Controller* organises and responds to all the interactions a user has with your app.__

The controller listens out for user input, performs whatever tasks it needs to, and sends output back to the user.

In our blog example, the controller might respond to requests for `www.myblog.com/` by retrieving a list of all posts and showing them in the homepage template. It would also have an action for `www.myblog.com/post/$id` that loads a specific post and it's comments, and displays them in the appropriate view. Finally it might respond to `www.myblog.com/post/new/` by showing a form, and if the user is logged in and the form is filled out correctly, saving a new post to the database.

Let's create a **BlogController.hx**, with an action for our homepage:

```haxe
class BlogController extends ufront.web.Controller {
		@:route("/")
		function homepage()
			return 'List all posts';
		
		@:route(GET,"/post/new/")
		function postForm()
			return "Show the post form";
		
		@:route(POST,"/post/new/")
		function savePost( args:{ title:String, text:String, date:String, authorID:Int } )
			return "Try save a new post";
		
		@:route("/post/$id")
		function showPost( id:Int )
			return 'Show post $id';
}
```

As you can see, in Ufront we define actions by creating functions on the controller.
Each action has a route, which ufront will use to figure out which URI addresses correspond with which actions.
In this very simple example we just return a `String`, this will display that output to the user.
Usually, you would return a view, or some data.

Ufront has lots of helpers in the `ufront.web.result` package to help format your result for the user.
The most common is the ViewResult, which we'll talk about next.'

So a controller has the task of *controlling* the user input, the models, the API and packaging up a view to send back to the user.
To learn more about using Controllers in Ufront, see [[03.0.0 Controller]].

#### Views

> __A *View* is all about how to display things to the user.__

This could be any technology that is capable of displaying a User Interface - a HTML page, an OpenFL interface, or even an email template.
In web apps it will often be a combination of HTML and CSS, usually using a templating system to insert the right data in the right places.

The reason to separate the view (display) from your model (data) is that you can update your view by editing a template file, and it doesn't risk breaking code elsewhere in your app - all you are doing is changing how the thing is displayed.
For example, if you have a backend developer and a graphic designer on your team, the backend developer could build all the code that interacts with the database, and the frontend developer can build the HTML to display the page, and you won't be stepping on each others toes.
Code and presentation remain separate.

Continuing the blog example, a View here might be a set of templates to render a blog homepage, a post page with comments, and an author's profile page.

In Ufront, the easiest way to render a view is to return a ViewResult from the controller (we'll explain controllers in a minute).
If you use a ViewResult like this:

```haxe
class BlogController extends ufront.web.Controller {
		@:route("/") function homepage() {
			return new ufront.web.result.ViewResult({
				siteTitle: "My (mostly abandoned) Blog",
				posts: BlogPost.manager.all(),
				author: Author.manager.select( $id==0 )
			});
		}
}
```

What we have done here is told ufront to find the default view for this action on the controller.
Using a little big of magic, Ufront will know to look for a view in "/views/blog/homepage.html".

If we were using the fairly fancy `erazor` templating system, our **homepage.html** could like like this:

```html
<h1>@siteTitle</h1>
<h2>Blog posts by @author.name and their friends.</h2>
@for (post in posts) {
	<article>
		<h3><a href="/posts/$post.id/">@post.title</a></h3>
		<h6>Written by @post.author.name on @post.date</h6>
		<div>
			@post.text
		</div>
	</article>
}
```

To learn more about using Views in Ufront, see [[04.0.0 View]]

#### APIs

> __An *API* shares the actions of your app that *must* run on the server, so they can be triggered from server, client, native app or anywhere.__

Ufront is built on Haxe, and Haxe can run pretty much anywhere - on the web server, in the visitor's web browser, on a command line, from a native phone app, etc.

Because all of these different *clients* might want to talk to our Ufront app, we share an API, which exposes some of our app's functions, so we can trigger that code to run on the server, not only from the server, but also from the client, or from a mobile app, or a CLI app etc.

Many web frameworks just use MVC, and any actions such as saving to a database or reading from the file system happen in the Controller, because the Controller only ever runs on the server.
Because we might want our code to run from a controller on the client, or from a command line task, or from a native phone app, anything that *must* run on the server goes in an API, and then we expose that API with Haxe remoting.
You can then call that API from wherever it's needed.
(On a side note, it also makes it easier to write an automated test suite - see [[11.0.0 Unit-testing]].)

With our blog example, we might have an API action for saving a new post. It might save the post to the database as well as email users who have subscribed to your blog. This action can now be triggered from wherever you want - not just the web page on the server. You could create a blog app that is capable of posting, using your Ufront API.
```haxe
class BlogApi extends ufront.api.UFApi {
	@inject public var mailer:UFMailer;
	public function savePost( post:BlogPost ) {
		post.save();
		for ( subscriber in Subscriber.manager.all() ) {
			var email = new Email()
				.to( subscriber.email )
				.from( post.author.email )
				.setSubject( 'New post on my blog: ${post.title}' )
				.setText( post.text );
			mailer.send( email );
		}
	}
}
```

Here all we are doing is saving the blog post to the database, and sending an email to each subscriber.
(For reference, the mailing support comes from the [ufront-mail](https://github.com/ufront/ufront-mail), and we are using dependency injection to grab whichever UFMailer we have set up for our app.)

So there's nothing special about writing APIs - you just put all the things that *must* run on the server in one API call.

We can then use it in our controllers, also with dependency injection:

```haxe
class BlogController extends ufront.web.Controller {
		
		// Load our API through dependency injection.
		@inject public var api:BlogApi;
		
		@:route(POST,"/post/new/")
		function savePost( args:{ title:String, text:String, date:String, authorID:Int } ) {
			// Create the post object from our user input.
			var post = new BlogPost();
			post.title = title;
			post.text = text;
			post.date = Date.fromString( date );
			post.authorID = authorID;
			
			// Use the API to save to our database and update our subscribers.
			api.savePost( post );
			
			return "Blog post saved!";
		}
}
```

To learn more about using APIs in Ufront, and sharing them with Haxe Remoting, see [[06.0.0 API]].

### Why